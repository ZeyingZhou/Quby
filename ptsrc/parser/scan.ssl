% PT Scanner Version 5.1
%
% Date:  30 Jun 1980
%       (Revised v5.1 - 8 Feb 2021)
%
% Author:  Alan Rosselet
%
% Abstract
%     The scanner breaks a PT source program up into syntax tokens.
%     It removes token separators - blanks, tabs, comments.  The
%     scanner detects these lexical errors:  unclosed comments,
%     unclosed literals, literals that cross a line boundary,
%     illegal characters and integers followed by identifiers with 
%     no separator between them.
%     Phase 1 Change notes: 
%     Add new Quby input character classes lPercent '%' lHash '#' lExclamation '! lQuestion '?' lDollar '$'
%     Reuse the existinrg IQuote character class to refer to the Quby double quote " instead of single quote '
%     Add new Quby syntax tokens for the Quby keywords pUsing pVal pDef pBreak pWhen pModule pUnless pElsif 
%     Remove old PT syntax tokens for the keywords pNot pUntil pProgram pConst pProcedure pBegin pRepeat
%     Add new Non Compound tokens for pDoubleEqual pNot pQuestion pDollor pHash
%     Replace the single quote char literals with the double quote "" 
%     Remove Compound tokens pStringLiteral, Add pLiteral
%     Remove '<>' pNotEqual, Add '!=' pNotEqual in rules, Add "==", "!", "?", "?" in rules
Input : 
        lLetter 
        firstCharClass = lLetter 
        lDigit
        lPlus           '+'
        lMinus          '-'
        lStar           '*'
        lColon          ':'
        lEquals         '='
        lDot            '.'
        lComma          ','
        lSemicolon      ';'
        lQuote          '"' %new
        lBackslash
        lLeftAngle      '<'
        lRightAngle     '>'
        lLeftParen      '('
        lRightParen     ')'
        lLeftBracket    '['
        lRightBracket   ']'
        lLeftBrace      '{'
        lRightBrace     '}'
        lBlank
        lIllegal
        lNewLine
        lCarriageReturn
        lTab
        lNewPage 
        lPercent        '%' %new
        lHash           '#' %new
        lExclamation    '!' %new
        lQuestion       '?' %new
        lDollar         '$' %new
        lEndFile
        lastCharClass = lEndFile;
       

Output :
        % Keyword tokens -
        %   Tokens pDiv through pRepeat are keywords which will be 
        %   separated from user defined identifiers by the Screener.
        %   This Scanner treats them all as identifiers.

        pDiv 
        firstKeywordToken = pDiv
        pMod
        pOr
        pAnd
        %pNot delete
        pThen
        pElse
        pOf
        pEnd
        %pUntil delete
        pDo
        pArray
        pFile
        %pProgram delete
        %pConst delete
        pVar
        pType
        %pProcedure delete
        %pBegin delete
        pIf
        pCase
        pWhile
        %pRepeat delete
        pUsing %new
        pDef %new
        pUnless %new
        pVal %new
        pElsif %new
        pBreak %new
        pWhen %new
        pModule %new
        lastKeywordToken = pModule
        
        % Scanner tokens -
        %   These are the tokens actually recognized by the Scanner itself.

        % Compound tokens - 
        %   These will be sent to the parser with a value or index.
        pIdentifier 
        firstCompoundInputToken = pIdentifier
        pInteger
        pStringLiteral %New
        lastCompoundInputToken = pStringLiteral

        % Non-compound tokens - 
        %   These tokens simply represent themselves.
        pNewLine
        pEndFile
        pPlus
        pMinus
        pStar
        %pColonEquals delete
        pDot
        pComma
        pSemicolon
        pColon
        pEquals
        pNotEqual
        pLess
        pLessEqual
        pGreaterEqual
        pGreater
        pLeftParen
        pRightParen
        pLeftBracket
        pRightBracket
        pDotDot
        pDoubleEqual %New
        pNot %New
        pQuestion %New
        pDollar %New
        pHash %New
        lastSyntaxToken = pHash;

Error :
        eIllegalChar = 5
        firstScanError = eIllegalChar
        eStringLiteralLine
        eStringLiteralEOF
        eCommentEOF
        eNoSeparator
        lastScanError = eNoSeparator;

mechanism Buffer :
    % Buffer saves the characters associated with the
    % compound tokens identifier, integer and literal.

    oBufferSave;
        % Save the character form of the last accepted lexical token.


rules

% Note that the order of choice alternatives
% may have a significant effect on performance
% under some implementations of S/SL.
Scan :
        {[
            | lBlank :
            | lLetter:
                oBufferSave
                @Identifier
            | lNewLine:
                .pNewLine
            | lCarriageReturn:
                % from Windows files, ignore
            | lTab:
            | ';':
                .pSemicolon
            | '(':
                .pLeftParen %new
         %  | '(':
         %      [
         %          | '*':
         %              @AlternateComment
         %          | *:
         %              .pLeftParen
         %      ]
            | ')':
                .pRightParen
            | '[':
                .pLeftBracket
            | ']':
                .pRightBracket
            | ',':
                .pComma
            | ':':
                .pColon % new
            %   [
            %       | '=':
            %           .pColonEquals
            %       | *:
            %           .pColon
            %   ]               
            | lDigit:
                oBufferSave
                @Integer
                [
                    | lLetter:
                        #eNoSeparator
                        oBufferSave
                        @Identifier
                    | *:
                ]
            | '=':
                .pEquals
            | '+':
                .pPlus
            | lQuote:
                @Literal
            | '-':
                .pMinus
            | '.':
                [
                    | '.':
                        .pDotDot
                    | *:
                        .pDot
                ]
            | '<':
                [
                 %  | '>':
                 %      .pNotEqual
                    | '=':
                        .pLessEqual
                    | *:
                        .pLess
                ]
            | '>':
                [
                    | '=':
                        .pGreaterEqual
                    | *:
                        .pGreater
                ]
         % | '{':
         %     @Comment
            | '%':
                @Comment
%%% new
            | '!':
                [
                    | '=':
                        .pNotEqual
                    | *:
                        .pNot
                ]
            | '?':
                .pQuestion
            | '$':
                .pDollar
            | '#':
                .pHash 
%%% new
            | '*':
                .pStar
            | lNewPage:
            | lEndFile:
                .pEndFile
                >
            | *:
                ?
                #eIllegalChar
        ]};

Identifier :
        % Assemble an identifier token

        {[
            | lLetter, lDigit:
                oBufferSave
            | *:
                .pIdentifier
                >
        ]};

Integer :
        % Assemble an integer token

        {[
            | lDigit:
                oBufferSave
            | *:
                .pInteger
                >
        ]};

Literal :
        % Assemble a literal string token

        {[
            | lQuote:
                [
                    | lQuote:
                        oBufferSave
                    | *:
                        .pStringLiteral
                        >
                ]
            | lNewLine:         % literal must not cross a line boundary
                #eStringLiteralLine
                .pNewLine
                >
            | lEndFile:         % literal must be closed before EOF
                #eStringLiteralEOF
                .pEndFile
                >
            | *:        % literal may contain any other character
                [
                    | lBackslash:
                        oBufferSave
                    | *:
                ]
                ?
                oBufferSave
        ]};

Comment :
        % Discard the contents of a comment

        {[
            | lNewLine: 
                .pNewLine
                >
       %    | '}':  Closed lRightBracket does not need any more
       %        >
       %    | lNewLine:
       %        .pNewLine
            | lEndFile:         % comment must be closed before EOF
                #eCommentEOF
                .pEndFile
                >
            | *:        % comment may contain any other character
                ?
        ]};
%%% delete
%AlternateComment :
%        {[
%            | '%':
%                >
%            | lNewLine:
%                .pNewLine
%            | lEndFile:
%                #eCommentEOF
%                .pEndFile
%                >
%            | *:
%                ?
%        ]};
%%% delete
end
